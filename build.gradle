buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.3.1'
        classpath 'org.sonarqube.gradle:gradle-sonarqube-plugin:1.0'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.4.0'
    }
}

repositories {
    jcenter()
    mavenLocal()
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.kt3k.coveralls'

version = file('VERSION').text.trim()

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.0"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
        versionCode generateVersionCode()
        versionName rootProject.version
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE.txt'
    }
}

dependencies {
    compile 'com.android.support:support-v13:23.0.0'
    compile 'io.reactivex:rxjava:1.0.14'
    provided 'net.danlew:android.joda:2.8.1'
    provided 'droidkit:droidkit-processor:5.2.2'
    testCompile 'junit:junit:4.12'
    testCompile('org.robolectric:robolectric:3.0') {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    testCompile 'org.mockito:mockito-core:2.0.31-beta'
    testCompile fileTree(dir: 'libs/test', include: ['*.jar'])
}

apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'

task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest']) {
    group = 'Reporting'
    description = 'Generate Jacoco coverage reports after running tests.'
    reports {
        xml {
            enabled = true
            destination = "${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml"
        }
    }

    // use hidden configuration, for details look into JacocoPlugin.groovy
    jacocoClasspath = project.configurations['androidJacocoAnt']

    sourceDirectories = fileTree(dir: "${project.projectDir}/src/main/java")
    classDirectories = fileTree(dir: "${project.buildDir}/intermediates/classes/debug", excludes: [
            '**/R.class',
            '**/R$*.class',
            '**/BuildConfig.*',
            '**/Manifest*.*',
            '**/*Test*.*',
            'android/**/*.*'
    ])
    executionData = fileTree(dir: project.buildDir, includes: ['**/*.exec', '**/*.ec'])
}

if (System.getenv('BINTRAY_USER') || project.hasProperty('bintrayUser')) {
    apply from: 'bintray.gradle'
}

if (System.getenv('CI_BUILD_REF')) {
    apply from: 'sonar.gradle'
}

int generateVersionCode() {
    def versionCode = 1
    (version =~ /(\d+)/).collect {
        Integer.parseInt(it[0])
    }.eachWithIndex { num, index ->
        versionCode += (num * (100000 / Math.pow(100, index)))
    }
    return versionCode as int
}