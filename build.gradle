buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.3.1'
        classpath 'org.sonarqube.gradle:gradle-sonarqube-plugin:1.0'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.4.0'
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

version = file('VERSION').text.trim()

ext {
    versionCode = calculateVersionCode()
    versionName = version
    compileSdkVersion = 22
    buildToolsVersion = "23.0.0"
}

int calculateVersionCode() {
    def versionCode = 1
    (version =~ /(\d+)/).collect {
        Integer.parseInt(it[0])
    }.eachWithIndex { num, index ->
        versionCode += (num * (100000 / Math.pow(100, index)))
    }
    return versionCode as int
}

apply plugin: 'org.sonarqube'

sonarqube {
    properties {
        properties['sonar.projectName'] = 'DroidKit'
        def buildRef = System.getenv('CI_BUILD_REF')
        if (buildRef != null && buildRef.length() > 8) {
            properties['sonar.projectVersion'] = project.version + '-' + buildRef.substring(0, 8)
        }
        properties['sonar.host.url'] = System.getenv('SONAR_HOST')
        properties['sonar.login'] = System.getenv('SONAR_LOGIN')
        properties['sonar.password'] = System.getenv('SONAR_PASSWORD')
        properties['sonar.jdbc.url'] = System.getenv('SONAR_JDBC_URL')
        properties['sonar.jdbc.username'] = System.getenv('SONAR_JDBC_USER')
        properties['sonar.jdbc.password'] = System.getenv('SONAR_JDBC_PASSWORD')
    }
}

project(':droidkit') {
    sonarqube {
        properties {
            properties["sonar.sources"] = android.sourceSets.main.java.srcDirs
            properties["sonar.tests"] = android.sourceSets.test.java.srcDirs
            properties["sonar.java.binaries"] = files(["${project.buildDir}/intermediates/classes/debug"])
            properties["sonar.java.test.binaries"] = files(["${project.buildDir}/intermediates/classes/test/debug"])
            properties["sonar.junit.reportsPath"] = "${project.buildDir}/test-results/debug"
            properties["sonar.jacoco.reportPath"] = "${project.buildDir}/jacoco/testDebugUnitTest.exec"
        }
    }
}