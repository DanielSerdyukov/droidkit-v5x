buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.0-beta3'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2'
        classpath 'org.sonarqube.gradle:gradle-sonarqube-plugin:1.0'
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

version = file('VERSION').text.trim()

ext {
    versionCode = calculateVersionCode()
    versionName = version
}

int calculateVersionCode() {
    int[] build = version.split('\\.').collect { Integer.parseInt(it) }
    return build[0] * 100000 + build[1] * 1000 + build[2]
}

apply plugin: 'org.sonarqube'

subprojects {
    apply plugin: 'jacoco'

    jacoco {
        toolVersion = '0.7.4.201502262128'
    }
}

sonarqube {
    properties {
        properties["sonar.projectName"] = 'DroidKit'
        def buildRef = System.getenv('CI_BUILD_REF')
        if (buildRef != null && buildRef.length() > 8) {
            properties["sonar.projectVersion"] = project.version + '-' + buildRef.substring(0, 8)
        }
        properties["sonar.host.url"] = System.getenv('SONAR_HOST')
        properties["sonar.login"] = System.getenv('SONAR_LOGIN')
        properties["sonar.password"] = System.getenv('SONAR_PASSWORD')
        properties["sonar.jdbc.url"] = System.getenv('SONAR_JDBC_URL')
        properties["sonar.jdbc.username"] = System.getenv('SONAR_JDBC_USER')
        properties["sonar.jdbc.password"] = System.getenv('SONAR_JDBC_PASSWORD')
    }
}

project(':library') {
    sonarqube {
        properties {
            properties["sonar.sources"] = android.sourceSets.main.java.srcDirs
            properties["sonar.tests"] = android.sourceSets.test.java.srcDirs
            properties["sonar.tests"] += android.sourceSets.androidTest.java.srcDirs
            properties["sonar.java.binaries"] = files(["${project.buildDir}/intermediates/classes/debug"])
            properties["sonar.java.test.binaries"] = files(["${project.buildDir}/intermediates/classes/test/debug"])
            properties["sonar.junit.reportsPath"] = "${project.buildDir}/test-results/debug"
            properties["sonar.jacoco.reportPath"] = "${project.buildDir}/jacoco/testDebugUnitTest.exec"
        }
    }
}

project(':apt') {
    sonarqube { skipProject = true }
}